pragma Singleton
pragma ComponentBehavior: Bound

import QtQuick
import Quickshell
import qs.DankCommon.Common

Singleton {
    function size(bytes) {
        if (bytes < 0)
            return "";
        const units = [I18n.tr("B", "file size unit"), I18n.tr("kB", "file size unit"), I18n.tr("MB", "file size unit"), I18n.tr("GB", "file size unit"), I18n.tr("TB", "file size unit")];
        let value = bytes;
        let unit = 0;
        while (value >= 1000 && unit < units.length - 1) {
            value /= 1000;
            unit++;
        }
        return `${value.toFixed(unit === 0 ? 0 : 1)} ${units[unit]}`;
    }

    function count(items) {
        if (items === 1)
            return I18n.tr("1 item", "a folder holding a single item");
        return I18n.tr("%1 items", "number of items in a folder").arg(items);
    }

    function modified(mtimeMs) {
        if (!mtimeMs)
            return "";
        return new Date(mtimeMs).toLocaleString(Qt.locale(), Locale.ShortFormat);
    }

    function typeLabel(entry) {
        switch (true) {
        case entry.isDir:
            return I18n.tr("Folder", "file type shown for a directory");
        case entry.symlinkBroken:
            return I18n.tr("Broken link", "file type shown for a symlink with no target");
        case entry.extension !== "":
            return entry.extension.toUpperCase();
        case entry.mime !== "":
            return entry.mime;
        default:
            return I18n.tr("File", "file type shown when nothing more precise is known");
        }
    }

    function listingError(code) {
        switch (code) {
        case "ENOENT":
            return I18n.tr("This folder no longer exists", "directory listing error");
        case "EACCES":
            return I18n.tr("You do not have permission to view this folder", "directory listing error");
        case "UNAVAILABLE":
            return I18n.tr("Files are unavailable because the file service is not running", "file browser placeholder when no backend is connected");
        default:
            return I18n.tr("This folder could not be opened", "directory listing error");
        }
    }

    function operationError(code, fallback) {
        switch (code) {
        case "ENOENT":
            return I18n.tr("This item no longer exists", "file operation error");
        case "EACCES":
            return I18n.tr("You do not have permission to do this", "file operation error");
        case "EEXIST":
            return I18n.tr("This item already exists", "file conflict dialog header");
        case "EBUSY":
            return I18n.tr("This item is in use", "file operation error");
        case "EXDEV":
            return I18n.tr("This item cannot be moved here directly", "file operation error");
        case "NOTSUPPORTED":
        case "":
            return fallback || I18n.tr("This item could not be handled", "file operation error");
        default:
            return I18n.tr("This item could not be handled", "file operation error");
        }
    }

    function validName(name) {
        const trimmed = name.trim();
        return trimmed !== "" && trimmed !== "." && trimmed !== ".." && !trimmed.includes("/");
    }
}
