import QtQuick
import QtQuick.Templates as T
import qs.DankCommon.Common

StyledRect {
    id: root

    LayoutMirroring.enabled: I18n.isRtl
    LayoutMirroring.childrenInherit: true

    KeyNavigation.tab: keyNavigationTab
    KeyNavigation.backtab: keyNavigationBacktab

    onActiveFocusChanged: {
        if (!activeFocus)
            return;
        textInput.forceActiveFocus();
    }

    property alias text: textInput.text
    property alias cursorPosition: textInput.cursorPosition
    property string placeholderText: ""
    property string labelText: ""
    property bool outlined: false
    property bool isError: false
    property string supportingText: ""
    property alias readOnly: textInput.readOnly
    property alias font: textInput.font
    property alias textColor: textInput.color
    property int echoMode: TextInput.Normal
    property alias validator: textInput.validator
    property alias maximumLength: textInput.maximumLength
    property string leftIconName: ""
    property int leftIconSize: Style.iconSize
    property color leftIconColor: Style.onSurfaceVariant
    property color leftIconFocusedColor: Style.primary
    property Component leadingContent: null
    property bool showClearButton: false
    property bool showPasswordToggle: false
    property real rightAccessoryWidth: 0
    property bool passwordVisible: false
    property bool usePopupTransparency: !Style.isFloatingWindow(root)
    property color backgroundColor: Style.chipSurface
    property color focusedBorderColor: Style.primary
    property color normalBorderColor: outlined ? Style.outline : Style.outlineVariant
    property color placeholderColor: Style.onSurfaceVariant
    property bool hidePlaceholderOnFocus: true
    property real borderWidth: Style.outlineWidth
    property real focusedBorderWidth: Style.outlineWidthFocused
    property real cornerRadius: Style.cornerRadiusXS
    property real controlHeight: Style.iconButtonSize

    readonly property real accessorySize: outlined ? Math.min(controlHeight, Style.fieldHeightLarge) : Style.buttonHeightXS
    readonly property real contentPadding: outlined ? Style.spacingL : Style.spacingM
    readonly property real leftPadding: {
        if (leadingLoader.item)
            return contentPadding + leadingLoader.width + Style.spacingS;
        if (outlined && leftIconName)
            return accessorySize + Style.spacingXS;
        return contentPadding + (leftIconName ? leftIconSize + contentPadding : 0);
    }
    readonly property real rightPadding: {
        let p = Style.spacingS + rightAccessoryWidth;
        if (showPasswordToggle)
            p += accessorySize + Style.spacingXS;
        if (showClearButton && text.length > 0)
            p += accessorySize + Style.spacingXS;
        return p;
    }
    property real topPadding: Style.spacingS
    property real bottomPadding: Style.spacingS
    property bool ignoreLeftRightKeys: false
    property bool ignoreUpDownKeys: false
    property bool ignoreTabKeys: false
    property var keyForwardTargets: []
    property Item keyNavigationTab: null
    property Item keyNavigationBacktab: null

    signal textEdited
    signal editingFinished
    signal accepted
    signal focusStateChanged(bool hasFocus)

    function getActiveFocus() {
        return textInput.activeFocus;
    }
    function setFocus(value) {
        textInput.focus = value;
    }
    function forceActiveFocus() {
        textInput.forceActiveFocus();
    }
    function selectAll() {
        textInput.selectAll();
    }
    function clear() {
        textInput.clear();
    }
    function insertText(str) {
        textInput.insert(textInput.cursorPosition, str);
    }

    readonly property real labelBandHeight: Math.round(Style.fontSizeSmall * 1.4) + Style.spacingXS * 2
    readonly property bool labelFloated: textInput.activeFocus || text.length > 0 || textInput.inputMethodComposing
    readonly property real labelProgress: Math.max(0, Math.min(1, labelMotion.value))
    readonly property real containerTop: outlined && labelText ? Style.outlinedFieldLabelLineHeight / 2 : 0
    readonly property real supportingHeight: supportingText ? supportingLabel.implicitHeight + Style.spacingXS : 0
    readonly property real containerHeight: height - containerTop - supportingHeight
    readonly property bool placeholderVisible: textInput.text.length === 0 && !textInput.inputMethodComposing && (outlined ? (!labelText || labelFloated) : (!hidePlaceholderOnFocus || !textInput.activeFocus))
    readonly property color outlineTargetColor: !enabled ? Style.onSurface_12 : isError ? (fieldHover.hovered && !textInput.activeFocus ? Style.onErrorContainer : Style.error) : textInput.activeFocus ? focusedBorderColor : fieldHover.hovered ? Style.onSurface : normalBorderColor
    readonly property real outlineStrokeWidth: !enabled ? borderWidth : borderWidth + (focusedBorderWidth - borderWidth) * Math.max(0, Math.min(1, strokeMotion.value))
    readonly property color labelTargetColor: !enabled ? Style.onSurface_38 : isError ? (fieldHover.hovered && !textInput.activeFocus ? Style.onErrorContainer : Style.error) : textInput.activeFocus ? Style.primary : fieldHover.hovered ? Style.onSurface : Style.onSurfaceVariant

    width: Style.fieldDefaultWidth
    implicitHeight: outlined ? Math.max(controlHeight, textInput.contentHeight + topPadding + bottomPadding) + containerTop + supportingHeight : Style.fieldHeight + (labelText ? labelBandHeight : 0)
    height: implicitHeight
    radius: cornerRadius
    color: outlined ? "transparent" : Style.foregroundColor(backgroundColor, !usePopupTransparency)
    border.color: textInput.activeFocus ? focusedBorderColor : normalBorderColor
    border.width: outlined ? 0 : textInput.activeFocus ? focusedBorderWidth : borderWidth

    Component.onCompleted: {
        labelMotion.snapTo(labelFloated ? 1 : 0);
        strokeMotion.snapTo(textInput.activeFocus ? 1 : 0);
        placeholderMotion.snapTo(placeholderVisible ? 1 : 0);
    }
    onLabelFloatedChanged: labelMotion.retarget(labelFloated ? 1 : 0)
    onEnabledChanged: {
        if (!enabled)
            strokeMotion.snapTo(0);
    }

    SpringMotion {
        id: labelMotion
        enabled: root.outlined && !Style.springMotionDisabled
        stiffness: Style.textFieldSpatialStiffness
        damping: 2 * Style.textFieldSpatialDampingRatio * Math.sqrt(stiffness)
    }

    SpringMotion {
        id: strokeMotion
        enabled: root.outlined && root.enabled && !Style.springMotionDisabled
        stiffness: labelMotion.stiffness
        damping: labelMotion.damping
    }

    HoverHandler {
        id: fieldHover
        enabled: root.enabled
        cursorShape: Qt.IBeamCursor
    }

    component FieldColor: QtObject {
        id: colorMotion
        property color target
        property color startColor: target
        property color endColor: target
        readonly property color value: Style._blend(startColor, endColor, Math.max(0, Math.min(1, progress.value)))
        onTargetChanged: {
            startColor = value;
            endColor = target;
            progress.snapTo(0);
            progress.retarget(1);
        }
        property SpringMotion progress: SpringMotion {
            value: 1
            enabled: root.outlined && root.enabled && !Style.springMotionDisabled
            stiffness: Style.textFieldFastEffectsStiffness
            damping: 2 * Math.sqrt(stiffness)
        }
    }

    FieldColor {
        id: outlinePaint
        target: root.outlineTargetColor
    }
    FieldColor {
        id: labelPaint
        target: root.labelTargetColor
    }
    readonly property color outlineColor: outlinePaint.value
    readonly property color labelColor: labelPaint.value
    readonly property real cutoutCenter: fieldLabel.x + fieldLabel.width / 2
    readonly property real cutoutHalfWidth: labelText ? (fieldLabel.width / 2 + Style.spacingXS) * labelProgress : 0
    readonly property real cutoutStart: Math.max(0, cutoutCenter - cutoutHalfWidth)
    readonly property real cutoutEnd: Math.min(width, cutoutCenter + cutoutHalfWidth)
    readonly property real labelTop: fieldLabel.y + (fieldLabel.height - labelGlyph.height * fieldLabel.textScale) / 2
    readonly property real cutoutBottom: containerTop + (labelText ? Math.max(0, containerTop + outlineStrokeWidth - labelTop) : 0)

    component FieldOutline: Rectangle {
        x: -parent.x
        y: root.containerTop - parent.y
        width: root.width
        height: root.containerHeight
        color: "transparent"
        radius: root.cornerRadius
        border.color: root.outlineColor
        border.width: root.outlineStrokeWidth
        antialiasing: true
    }

    Item {
        width: root.cutoutStart
        height: root.cutoutBottom
        clip: true
        visible: root.outlined
        FieldOutline {}
    }

    Item {
        x: root.cutoutEnd
        width: root.width - x
        height: root.cutoutBottom
        clip: true
        visible: root.outlined
        FieldOutline {}
    }

    Item {
        y: root.cutoutBottom
        width: root.width
        height: Math.max(0, root.containerTop + root.containerHeight - y)
        clip: true
        visible: root.outlined
        FieldOutline {}
    }

    MouseArea {
        anchors.fill: parent
        enabled: root.outlined && root.enabled
        onPressed: root.forceActiveFocus()
    }

    DankIcon {
        id: leftIcon

        anchors.left: parent.left
        anchors.leftMargin: root.outlined ? (root.accessorySize - root.leftIconSize) / 2 : root.contentPadding
        anchors.verticalCenter: textInput.verticalCenter
        name: leftIconName
        size: leftIconSize
        color: root.outlined ? (root.enabled ? root.leftIconColor : Style.onSurface_38) : textInput.activeFocus ? leftIconFocusedColor : leftIconColor
        visible: leftIconName !== "" && !leadingLoader.item
    }

    Loader {
        id: leadingLoader

        anchors.left: parent.left
        anchors.leftMargin: root.contentPadding
        anchors.verticalCenter: textInput.verticalCenter
        active: root.leadingContent !== null
        sourceComponent: root.leadingContent
    }

    Item {
        id: fieldLabel

        readonly property real textScale: root.outlined ? 1 + (Style.fontSizeSmall / labelGlyph.font.pixelSize - 1) * root.labelProgress : 1

        anchors.left: parent.left
        anchors.leftMargin: root.outlined ? root.contentPadding + (root.leftPadding - root.contentPadding) * (1 - root.labelProgress) : root.leftPadding
        y: root.outlined ? root.containerTop + (root.containerHeight / 2) * (1 - root.labelProgress) - height / 2 : Style.spacingXS
        implicitWidth: labelGlyph.width * textScale
        width: implicitWidth
        height: root.outlined ? root.font.pixelSize + Style.spacingS + (Style.outlinedFieldLabelLineHeight - root.font.pixelSize - Style.spacingS) * root.labelProgress : Style.outlinedFieldLabelLineHeight
        visible: root.labelText !== ""

        StyledText {
            id: labelGlyph

            anchors.left: parent.left
            anchors.verticalCenter: parent.verticalCenter
            anchors.alignWhenCentered: false
            width: implicitWidth
            text: root.labelText
            font.pixelSize: root.outlined ? root.font.pixelSize : Style.fontSizeSmall
            scale: fieldLabel.textScale
            transformOrigin: I18n.isRtl ? Item.Right : Item.Left
            color: root.outlined ? root.labelColor : textInput.activeFocus ? Style.primary : Style.onSurfaceVariant
            wrapMode: Text.NoWrap
            maximumLineCount: 1
            elide: Text.ElideNone
        }
    }

    T.TextField {
        id: textInput

        background: null
        padding: 0

        anchors.left: parent.left
        anchors.leftMargin: root.leftPadding
        anchors.right: rightButtonsRow.visible ? rightButtonsRow.left : parent.right
        anchors.rightMargin: rightButtonsRow.visible ? Style.spacingS : root.contentPadding + root.rightAccessoryWidth
        anchors.top: parent.top
        anchors.topMargin: root.outlined ? root.containerTop + root.topPadding : root.labelText !== "" ? root.labelBandHeight : root.topPadding
        anchors.bottom: parent.bottom
        anchors.bottomMargin: root.bottomPadding + (root.outlined ? root.supportingHeight : 0)
        font.pixelSize: Style.fontSizeMedium
        font.family: Style.fontFamily
        font.weight: Style.fontWeight
        color: root.outlined && !root.enabled ? Style.onSurface_38 : Style.surfaceText
        selectionColor: Style.selectedContainer
        selectedTextColor: Style.onSelectedContainer
        horizontalAlignment: TextInput.AlignLeft
        verticalAlignment: TextInput.AlignVCenter
        selectByMouse: !root.ignoreLeftRightKeys
        echoMode: root.passwordVisible && root.echoMode === TextInput.Password ? TextInput.Normal : root.echoMode
        clip: true
        activeFocusOnTab: root.enabled
        Accessible.name: root.Accessible.name || root.labelText || root.placeholderText
        Accessible.description: root.supportingText || root.Accessible.description
        cursorDelegate: DankTextCursor {
            id: fieldCursor

            color: root.outlined ? (root.isError ? Style.error : Style.primary) : textInput.color
            x: textInput.cursorRectangle.x
            y: textInput.cursorRectangle.y
            height: textInput.cursorRectangle.height
            shown: textInput.cursorVisible

            readonly property int inputCursorPosition: textInput.cursorPosition
            readonly property string inputText: textInput.text

            onInputCursorPositionChanged: resetBlink()
            onInputTextChanged: resetBlink()
        }
        KeyNavigation.tab: root.keyNavigationTab
        KeyNavigation.backtab: root.keyNavigationBacktab
        onTextChanged: root.textEdited()
        onEditingFinished: root.editingFinished()
        onAccepted: root.accepted()
        onActiveFocusChanged: {
            strokeMotion.retarget(activeFocus ? 1 : 0);
            root.focusStateChanged(activeFocus);
        }
        Keys.forwardTo: root.keyForwardTargets
        Keys.onLeftPressed: event => {
            event.accepted = root.ignoreLeftRightKeys;
        }
        Keys.onRightPressed: event => {
            event.accepted = root.ignoreLeftRightKeys;
        }
        Keys.onPressed: event => {
            if (root.ignoreTabKeys && (event.key === Qt.Key_Tab || event.key === Qt.Key_Backtab)) {
                event.accepted = false;
                for (var i = 0; i < root.keyForwardTargets.length; i++) {
                    if (root.keyForwardTargets[i])
                        root.keyForwardTargets[i].Keys.pressed(event);
                }
                return;
            }
            if (root.ignoreUpDownKeys && (event.key === Qt.Key_Up || event.key === Qt.Key_Down)) {
                event.accepted = false;
                for (var i = 0; i < root.keyForwardTargets.length; i++) {
                    if (root.keyForwardTargets[i])
                        root.keyForwardTargets[i].Keys.pressed(event);
                }
                return;
            }
            if ((event.modifiers & (Qt.ControlModifier | Qt.AltModifier | Qt.MetaModifier)) && root.keyForwardTargets.length > 0) {
                for (var i = 0; i < root.keyForwardTargets.length; i++) {
                    if (root.keyForwardTargets[i])
                        root.keyForwardTargets[i].Keys.pressed(event);
                }
            }
        }

        MouseArea {
            anchors.fill: parent
            hoverEnabled: true
            cursorShape: Qt.IBeamCursor
            acceptedButtons: Qt.NoButton
        }
    }

    Row {
        id: rightButtonsRow

        anchors.right: parent.right
        anchors.rightMargin: (root.outlined ? Style.spacingXS : Style.spacingS) + root.rightAccessoryWidth
        anchors.verticalCenter: textInput.verticalCenter
        spacing: root.outlined ? 0 : Style.spacingXS
        visible: showPasswordToggle || (showClearButton && !readOnly && text.length > 0)

        Loader {
            active: root.showPasswordToggle
            visible: active
            sourceComponent: DankActionButton {
                focusPolicy: Qt.TabFocus
                checkable: true
                checked: root.passwordVisible
                Accessible.name: root.passwordVisible ? I18n.tr("Hide password", "Accessible name for the password visibility button") : I18n.tr("Show password", "Accessible name for the password visibility button")
                buttonSize: root.accessorySize
                iconName: root.passwordVisible ? "visibility_off" : "visibility"
                iconSize: root.outlined ? Style.iconSize : Style.iconSizeSmall
                iconColor: root.outlined && root.isError ? (fieldHover.hovered && !textInput.activeFocus ? Style.onErrorContainer : Style.error) : Style.onSurfaceVariant
                onClicked: root.passwordVisible = !root.passwordVisible
            }
        }

        Loader {
            active: root.showClearButton && !root.readOnly
            visible: active && root.text.length > 0
            sourceComponent: DankActionButton {
                focusPolicy: Qt.TabFocus
                Accessible.name: I18n.tr("Clear")
                buttonSize: root.accessorySize
                iconName: "close"
                iconSize: root.outlined ? Style.iconSize : Style.iconSizeSmall
                iconColor: root.outlined && root.isError ? (fieldHover.hovered && !textInput.activeFocus ? Style.onErrorContainer : Style.error) : Style.onSurfaceVariant
                onClicked: textInput.text = ""
            }
        }
    }

    Item {
        anchors.fill: textInput
        visible: textInput.text.length === 0 && !textInput.inputMethodComposing && opacity > 0
        opacity: root.outlined ? Math.max(0, Math.min(1, placeholderMotion.value)) : root.placeholderVisible ? 1 : 0

        StyledText {
            anchors.fill: parent
            text: root.placeholderText
            font: textInput.font
            color: root.outlined && !root.enabled ? Style.onSurface_38 : placeholderColor
            horizontalAlignment: Text.AlignLeft
            verticalAlignment: textInput.verticalAlignment
            wrapMode: Text.NoWrap
            maximumLineCount: 1
            elide: I18n.isRtl ? Text.ElideLeft : Text.ElideRight
        }
    }

    StyledText {
        id: supportingLabel
        anchors.left: parent.left
        anchors.right: parent.right
        anchors.bottom: parent.bottom
        anchors.leftMargin: root.contentPadding
        anchors.rightMargin: root.contentPadding
        visible: root.outlined && root.supportingText !== ""
        text: root.supportingText
        font.pixelSize: Style.fontSizeSmall
        lineHeightMode: Text.FixedHeight
        lineHeight: Style.outlinedFieldLabelLineHeight
        color: !root.enabled ? Style.onSurface_38 : root.isError ? Style.error : Style.onSurfaceVariant
        wrapMode: Text.WordWrap
    }

    SpringMotion {
        id: placeholderMotion
        enabled: root.outlined && !Style.springMotionDisabled
        stiffness: root.placeholderVisible ? Style.textFieldSlowEffectsStiffness : Style.textFieldFastEffectsStiffness
        damping: 2 * Math.sqrt(stiffness)
    }

    onPlaceholderVisibleChanged: placeholderMotion.retarget(placeholderVisible ? 1 : 0)

    Behavior on border.color {
        enabled: Style.currentAnimationSpeed !== Style.AnimationSpeed.None
        DankColorAnim {
            duration: Style.expressiveDurations.expressiveEffects
            easing.bezierCurve: Style.expressiveCurves.expressiveEffects
        }
    }

    Behavior on border.width {
        enabled: Style.currentAnimationSpeed !== Style.AnimationSpeed.None
        DankAnim {
            duration: Style.expressiveDurations.expressiveEffects
            easing.bezierCurve: Style.expressiveCurves.expressiveEffects
        }
    }
}
