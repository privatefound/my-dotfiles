import QtQuick
import QtQuick.Controls
import QtQuick.Window
import qs.DankCommon.Common

Item {
    id: root

    property string text: ""
    property alias delay: tooltip.delay
    property Item sourceItem: null
    readonly property var sourceWindow: sourceItem?.Window.window ?? null

    onSourceItemChanged: {
        if (!sourceItem)
            tooltip.close();
    }

    function show(text, item, offsetX, offsetY, preferredSide) {
        if (!item || !item.visible) {
            hide();
            return;
        }

        let windowContentItem = item.Window?.window?.contentItem;
        if (!windowContentItem) {
            let current = item;
            while (current) {
                if (current.Window?.window?.contentItem) {
                    windowContentItem = current.Window.window.contentItem;
                    break;
                }
                current = current.parent;
            }
        }
        if (!windowContentItem)
            return;

        root.sourceItem = item;
        tooltip.parent = windowContentItem;
        tooltip.text = text;

        const itemPos = item.mapToItem(windowContentItem, 0, 0);
        const parentWidth = windowContentItem.width;
        const parentHeight = windowContentItem.height;
        const tooltipWidth = tooltip.implicitWidth;
        const tooltipHeight = tooltip.implicitHeight;
        const gap = Style.spacingS;

        const side = preferredSide || _determineBestSide(itemPos, item, parentWidth, parentHeight, tooltipWidth, tooltipHeight);

        let targetX = 0;
        let targetY = 0;

        switch (side) {
        case "left":
            targetX = itemPos.x - tooltipWidth - gap;
            targetY = itemPos.y + (item.height - tooltipHeight) / 2;
            break;
        case "right":
            targetX = itemPos.x + item.width + gap;
            targetY = itemPos.y + (item.height - tooltipHeight) / 2;
            break;
        case "top":
            targetX = itemPos.x + (item.width - tooltipWidth) / 2;
            targetY = itemPos.y - tooltipHeight - gap;
            break;
        case "bottom":
        default:
            targetX = itemPos.x + (item.width - tooltipWidth) / 2;
            targetY = itemPos.y + item.height + gap;
            break;
        }

        tooltip.x = Math.max(Style.spacingXS, Math.min(parentWidth - tooltipWidth - Style.spacingXS, targetX + (offsetX || 0)));
        tooltip.y = Math.max(Style.spacingXS, Math.min(parentHeight - tooltipHeight - Style.spacingXS, targetY + (offsetY || 0)));

        tooltip.open();
    }

    function _determineBestSide(itemPos, item, parentWidth, parentHeight, tooltipWidth, tooltipHeight) {
        const itemCenterX = itemPos.x + item.width / 2;
        const margin = Style.spacingL;

        const spaceLeft = itemPos.x;
        const spaceRight = parentWidth - (itemPos.x + item.width);
        const spaceTop = itemPos.y;
        const spaceBottom = parentHeight - (itemPos.y + item.height);

        if (spaceRight >= tooltipWidth + margin)
            return "right";
        if (spaceLeft >= tooltipWidth + margin)
            return "left";
        if (spaceBottom >= tooltipHeight + margin)
            return "bottom";
        if (spaceTop >= tooltipHeight + margin)
            return "top";
        if (itemCenterX > parentWidth / 2)
            return "left";
        return "right";
    }

    function hide() {
        tooltip.close();
        sourceItem = null;
    }

    onVisibleChanged: {
        if (!visible)
            hide();
    }
    Component.onDestruction: tooltip.close()

    Connections {
        target: root.sourceItem
        function onVisibleChanged() {
            if (!root.sourceItem?.visible)
                root.hide();
        }
    }

    Connections {
        target: root.sourceWindow
        function onVisibleChanged() {
            if (!root.sourceWindow?.visible)
                root.hide();
        }
    }

    ToolTip {
        id: tooltip

        leftPadding: Style.spacingM
        rightPadding: Style.spacingM
        topPadding: Style.spacingS
        bottomPadding: Style.spacingS
        margins: Style.spacingXS
        closePolicy: Popup.NoAutoClose
        modal: false
        dim: false

        Binding {
            target: tooltip.contentItem?.parent ?? null
            property: "containmentMask"
            value: QtObject {
                function contains(position: point): bool {
                    return false;
                }
            }
        }

        background: Rectangle {
            color: Style.inverseSurface
            radius: Style.cornerRadiusXS
        }

        contentItem: Text {
            id: textContent

            width: Math.min(implicitWidth, Style.tooltipMaxWidth)
            text: tooltip.text
            font.pixelSize: Style.fontSizeSmall
            font.family: Style.fontFamily
            color: Style.inverseOnSurface
            wrapMode: Text.NoWrap
            maximumLineCount: 1
            elide: Text.ElideRight
            horizontalAlignment: Text.AlignHCenter
            verticalAlignment: Text.AlignVCenter
        }

        enter: Transition {
            enabled: !Style.reduceMotion && Style.currentAnimationSpeed !== Style.AnimationSpeed.None
            DankAnim {
                property: "opacity"
                from: 0
                to: 1
                duration: Style.expressiveDurations.expressiveEffects
                easing.bezierCurve: Style.expressiveCurves.expressiveEffects
            }
        }

        exit: Transition {
            enabled: !Style.reduceMotion && Style.currentAnimationSpeed !== Style.AnimationSpeed.None
            DankAnim {
                property: "opacity"
                from: 1
                to: 0
                duration: Style.shorterDuration
                easing.bezierCurve: Style.expressiveCurves.expressiveEffects
            }
        }
    }
}
